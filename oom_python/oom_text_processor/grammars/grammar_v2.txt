sentence -> descriptive | possessive | behavioral | singleton

descriptive -> protected entity | entity sealed | entity abstract | entity desc_verb numeral_indicator inherited_entity
inherited_entity -> entity

possessive -> defining_possessed | possessed_limitation | possessed_description

defining_possessed -> entity pos_verb possessed | private_indicator entity pos_verb possessed

possessed_limitation -> limited_indicator entity pos_indicator possessed
limited_indicator -> private | protected

possessed_description -> entity pos_indicator possessed desc_verb description_type
description_type -> entity | numeral_indicator entity | constant | static

possessed -> pos_object | numeral_indicator pos_object

behavioral -> entity behav_verb behavior | private_indicator entity behav_verb behavior | entity behav_verb behavior abstractly_keyword
behavior -> behav_object | behav_object parameter_enumeration | behav_object involved_indicator parameter_enumeration
parameter_enumeration -> parameter | parameter enumeration_indicator parameter_enumeration
parameter -> entity | numeral_indicator entity

singleton -> there_keyword desc_verb private_indicator one_keyword entity

protected -> negation_indicator everyone_keyword ack_verb
private -> negation_indicator one_keyword ack_verb
sealed -> pos_verb negation_indicator heir_keyword
abstract -> desc_verb abstract_keyword
constant -> "unchangeable"
static -> "shareable"

private_indicator -> "only"
pos_indicator -> "POS"
involved_indicator -> "with"
enumeration_indicator -> "and"
numeral_indicator -> "a" | "an" | "many"

negation_indicator -> "no" | "not"

desc_verb -> "be"
pos_verb -> "have"
behav_verb -> "can" | "should" | "do" | "may"
ack_verb -> "know"

entity -> "NN" | "NNS" | "NNP" | "NNPS" | "JJ" | "JJR" | "JJS" | "FW" | "VB" | "VBG" | "VBD" | "VBN" | "VBP" | "VBZ" | "DT"
pos_object -> "NN" | "NNS" | "NNP" | "NNPS" | "JJ" | "JJR" | "JJS" | "FW" | "VB" | "VBG" | "VBD" | "VBN" | "VBP" | "VBZ" | "DT"
behav_object -> "VB" | "VBG" | "VBD" | "VBN" | "VBP" | "VBZ" | "FW" | abstractly

everyone_keyword -> "everyone"
one_keyword -> "one"
abstract_keyword -> "abstract"
abstractly_keyword -> "abstractly"
heir_keyword -> "heir"
there_keyword -> "there"

